def channel = "testing"
def version = "0.0.0"
def packageName = "GTestAllureUtilities"
def testApplications = ["UnitTest","IntegrationTest"]
def profile = "vs2022.conanprofile"
def archs = ['x86', 'x86_64']
def configs = ['Debug', 'Release']

library identifier: "cpp-jenkins-pipelines@master", retriever: modernSCM(
  [$class: "GitSCMSource",
   remote: "https://github.com/systelab/cpp-jenkins-pipelines.git",
   credentialsId: "GitHubCredentials"])
   
pipeline
{
	agent
	{
		label 'lib-build'
	}

	parameters
	{
		booleanParam( name: 'uploadTestingPkg',
					  description: 'Whether or not to upload testing conan package',
					  defaultValue: false )
	}

	options
	{
		skipDefaultCheckout(true)
		disableConcurrentBuilds()
		buildDiscarder(logRotator(numToKeepStr: '5'))
	}

	stages
	{
		stage('Checkout')
		{
			steps
			{
				deleteDir()
				checkoutSourceCode()
			}
		}
		
		stage('Build')
		{	
			steps
			{
				script
				{
					archs.each
					{ arch ->
						configs.each
						{ config ->
							stage("Build ${config}|${arch}")
							{
								def buildFolder = "build-${config}-${arch}"
								bat "conan install . --install-folder ${buildFolder} --profile=${profile} -s build_type=${config} -s arch=${arch}"
								bat "conan build . --build-folder ${buildFolder}"
								bat "conan export-pkg . ${packageName}/${version}@systelab/${channel} --profile=${profile} -s build_type=${config} -s arch=${arch} --force"
								bat "conan test test_package/conanfile.py ${packageName}/${version}@systelab/${channel} --test-build-folder test_package/$buildFolder --profile=${profile} -s build_type=${config} -s arch=${arch}"
								dir("${buildFolder}/bin/${config}")
								{
									testApplications.each
									{ testApplication ->
										bat "${testApplication}.exe --gtest_output=xml:${env.WORKSPACE}/${buildFolder}/test_reports/${testApplication}.xml"
									}
								}
							}
						}
					}
				}
			}
		}

		stage('Deploy')
		{
			when
			{
				expression { return params.uploadTestingPkg }
			}
			steps
			{
				bat "conan upload ${packageName}/${version}@systelab/${channel} --all -r systelab-conan-local --force"
			}
		}
	}

	post
	{
		always
		{
			junit allowEmptyResults: true, testResults: "build*/test_reports/*.xml"
		}
	}
}
